grammar hu.bme.mit.DepModel with org.eclipse.xtext.common.Terminals

generate depModel "http://www.bme.hu/mit/DepModel"

DepModel:
	(elements+=AbstractElement)*;

QualifiedName:
	ID ('.' ID)*;

AbstractElement:
	ComponentModelDec | PortType | ComponentType;

PortType:
	'port' 'type' name=QualifiedName '{'
	(eModes+=ErrorModes)+
	'}';

ErrorModes:
	'error' 'mode' name=QualifiedName;

	//Component Model ********************************************************************
ComponentModelDec:
	SystemConnDec | SystemDec;

SystemConnDec:
	 'connection' name=QualifiedName 'from' sourceSystem=[SystemDec] '.' sourcePort=[SystemPortOut]
	'to' targetSystem=[SystemDec] '.' targetPort=[SystemPortIn] 'with' prob=DOUBLE;

SystemDec:
	'system'  name=QualifiedName '{'
	(sysFeatures+=SysFeaturesDec)*
	'}';

SysFeaturesDec:
	SystemPortDec | ComponentConnDec | ComponentImpl;

SystemPortDec:
	SystemPortIn | SystemPortOut;

SystemPortIn:
	'system' 'port' 'in' name=QualifiedName 'to' inComp=[ComponentImpl] '.' inPort=[PortIn];

SystemPortOut:
	'system' 'port' 'out' name=QualifiedName 'from' outComp=[ComponentImpl] '.' outPort=[PortOut];

ComponentConnDec:
	 'connection' name=QualifiedName 'from' sourceComp=[ComponentImpl] '.' sourcePort=[PortOut]
	'to' targetComp=[ComponentImpl] '.' targetPort=[PortIn] 'with' prob=DOUBLE;

ComponentImpl:
	'component'  name=QualifiedName ':' superType=[ComponentType];

ComponentType:
	'component' 'type' name=QualifiedName '{'
	(componentFeatures+=ComponentFeaturesDec)*
	'}';

ComponentFeaturesDec:
	PortDec | ErrorModelDec;

PortDec:
	PortIn | PortOut;

PortIn:
	'port'  'in' name=QualifiedName ':' PortInSuperType=[PortType];

PortOut:
	'port'  'out' name=QualifiedName ':' PortOutSuperType=[PortType];

	//Error Model *********************************
ErrorModelDec:
	'error' 'model' name=QualifiedName '{'
	(emodelElements+=ErrorModelElement)*
	'}';

ErrorModelElement:
	State | TransitionDec;

State:
	('initial')?  'state' name=QualifiedName;

TransitionDec:
	'transition' name=QualifiedName '{'
	(transFeatures+=TransitionFeatureDec)*
	'}';

TransitionFeatureDec:
	TransitionState | Trigger | Action | "occurrence" {Occurrence} DOUBLE;

TransitionState:
	'states' sourceState=[State] '->' targetState=[State];

Trigger:
	'trigger'  portInstance=[PortIn] '.' errorMode=[ErrorModes];

Action:
	'action' portInstance=[PortOut] '.' errorMode=[ErrorModes];

DOUBLE:
	INT '.' INT;
