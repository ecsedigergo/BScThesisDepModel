grammar hu.bme.mit.DepModel with org.eclipse.xtext.common.Terminals

generate depModel "http://www.bme.hu/mit/DepModel"

DepModel:
	(elements+=AbstractElement)*;

QualifiedName:
	ID ('.' ID)*;

AbstractElement:
	ComponentModelDec | PortType;

PortType:
	'port' 'type' name=QualifiedName '{'
	(eModes+=ErrorModes)*
	'}';

ErrorModes:
	'error' 'mode' name=QualifiedName;

	//Component Model ********************************************************************
ComponentModelDec:
	SystemConnDec | SystemDec ;

SystemConnDec:
	'system' 'connection' name=QualifiedName 'from' sourceSystem=[SystemDec] '.' sourcePort=[SystemPortDec]
	'to' targetSystem=[SystemDec] '.' targetPort=[SystemPortDec];

SystemDec:
	'system' 'declaration' name=QualifiedName '{'
	(sysFeatures+=SysFeaturesDec)*
	'}';

SysFeaturesDec:
	SystemPortDec | ComponentConnDec | ComponentType | ComponentImpl  ;
	//ComponentType;

SystemPortDec:
	SystemPortIn | SystemPortOut;

SystemPortIn:
	'system' 'port' 'in' name=QualifiedName 'to' inComp=[ComponentImpl] '.' inPort=[PortIn];

SystemPortOut:
	'system' 'port' 'out' name=QualifiedName 'from' outComp=[ComponentImpl] '.' outPort=[PortOut];

ComponentConnDec:
	'component' 'connection' name=ID 'from' sourceComp=[ComponentImpl] '.' sourcePort=[PortOut]
	'to' targetComp=[ComponentImpl] '.' targetPort=[PortIn];

ComponentImpl:
	'component' 'implementation' name=QualifiedName ':' superType=[ComponentType];

ComponentType:
	'component' 'type' name=QualifiedName '{'
	(componentFeatures+=ComponentFeaturesDec)*
	'}';

ComponentFeaturesDec:
	PortDec | ErrorModelDec;

PortDec:
	PortIn | PortOut
;

PortIn:
	'port' 'instance' 'in' name=QualifiedName ':' PortInSuperType=[PortType];

PortOut:
	'port' 'instance' 'out' name=QualifiedName ':' PortOutSuperType=[PortType];


//Error Model *********************************
ErrorModelDec:
	'error' 'model' name=QualifiedName '{'
	(emodelElements+=ErrorModelElement)*
	'}';

ErrorModelElement:
	StateDec | TransitionDec | ErrorPortDec;

ErrorPortDec:
	ErrorPortIn | ErrorPortOut
;

ErrorPortIn:
	'error' 'port' 'in' name=QualifiedName ':' ErrorPortInSuperType=[PortType]
;

ErrorPortOut:
	'error' 'port' 'out' name=QualifiedName ':' ErrorPortOutSuperType=[PortType]
;

StateDec:
	'error' 'state' name=QualifiedName;

TransitionDec:
	'transition' name=QualifiedName '{'
	(Features+=TransitionFeatureDec)*
	'}';

TransitionFeatureDec:
	TransitionState | TriggerDec | ActionDec | "occurrence" {OccurrenceDec} DOUBLE;

TransitionState:
	'states' sourceState=[StateDec] '->' targetState=[StateDec];

TriggerDec:
	'trigger' name=QualifiedName ':' portInstance=[PortDec] '.' errorMode=[ErrorModes];

ActionDec:
	'action' name=QualifiedName ':' portInstance=[PortDec] '.' errorMode=[ErrorModes];

DOUBLE:
	INT '.' INT;
